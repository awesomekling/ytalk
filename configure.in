dnl configure.in for YTalk
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.50)

AC_INIT(YTalk, 3.2.0)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE

AC_REVISION($LastChangedRevision: 79 $)
AM_CONFIG_HEADER(config.h)

dnl Do some system detection. Don't expand this without a good reason.
host=`uname -s`
case "$host" in
*SunOS*)
  SYSTEM_DEFS="-DYTALK_SUNOS"
  ;;
*HP-UX*)
  SYSTEM_DEFS="-DYTALK_HPUX"
  ;;
*OSF1*)
  SYSTEM_DEFS="-DYTALK_OSF1"
  ;;
esac
AC_SUBST(SYSTEM_DEFS)

AC_DEFINE_DIR([sysconfdir], [sysconfdir], [sysconfdir])
AC_DEFINE_UNQUOTED(SYSTEM_YTALKRC, ["${sysconfdir}/ytalkrc"], ["Location of default ytalkrc file"])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Sanity
AC_CHECK_SIZEOF(long)

ytalk_cv_curses=/usr
AC_ARG_WITH(curses, [  --with-curses=DIR       Where ncurses is installed ],
	[if test $withval != yes; then
		ytalk_cv_curses=$withval
	fi
	if test x$ytalk_cv_curses != x/usr; then
		LDFLAGS="-L${ytalk_cv_curses}/lib $LDFLAGS"
		CPPFLAGS="$CPPFLAGS -I${ytalk_cv_curses}/include"
	fi])

AC_CHECK_LIB(ncursesw, initscr,
	[LIBS="$LIBS -lncursesw"
	if test x$ytalk_cv_curses = x/usr -a -d /usr/include/ncursesw; then
		CPPFLAGS="$CPPFLAGS -I/usr/include/ncursesw"
	fi
	AC_CHECK_HEADERS(ncurses.h)],[
	ac_widec_possible=no
	AC_CHECK_LIB(ncurses, initscr,
		[LIBS="$LIBS -lncurses"
		if test x$ytalk_cv_curses = x/usr -a -d /usr/include/ncurses; then
			CPPFLAGS="$CPPFLAGS -I/usr/include/ncurses"
		fi
		AC_CHECK_HEADERS(ncurses.h)],
		[CF_CURSES_LIBS])
	])

dnl Look for termios, and if that fails, try sgtty.
AC_CHECK_FUNC(tcgetattr, [\
  AC_CHECK_HEADERS(termios.h,, no_termios=1)], no_termios=1)
if test "x$no_termios" = "x1" ; then 
  AC_CHECK_HEADERS(sgtty.h,, AC_MSG_ERROR(Cannot find either termios or sgtty))
fi

dnl X setup.
AC_ARG_WITH(x, 
  AC_HELP_STRING([--with-x], [use the X Window System]),
  no_x=0, no_x=1)
if test "x$no_x" = "x1"; then
  AC_DEFINE([X_DISPLAY_MISSING])
  XLIBS=""
  XINCS=""
else
  AC_PATH_XTRA
  if test "x$X_LIBS" = "x"; then
    echo "No X found but --with-x stated, aborting!"
    exit 1
  fi
  XLIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
  XINCS="$X_CFLAGS"
  AC_LIBOBJ([xwin])
fi
AC_SUBST(XLIBS)
AC_SUBST(XINCS)

dnl Some systems need extra libraries for sockets and dns...
AC_CHECK_FUNC(socket,,AC_CHECK_LIB(socket, socket,, [AC_MSG_ERROR(Can't compile without socket)]))
AC_CHECK_FUNC(gethostbyname,,AC_CHECK_LIB(nsl, gethostbyname,, [AC_MSG_ERROR(Can't compile without gethostbyname)]))

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h netdb.h netinet/in.h sys/ioctl.h sys/time.h unistd.h stropts.h)
AC_CHECK_HEADERS(iovec.h sys/uio.h stdlib.h string.h sys/param.h arpa/inet.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_HEADER_TIME
AC_TYPE_UID_T

AC_CHECK_TYPE([socklen_t], [AC_DEFINE(HAVE_SOCKLEN_T, 1, [Define to 1 if you have the `socklen_t` type])], [], [
	#include <sys/types.h>
	#include <sys/socket.h>])

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(putenv strerror ttyname ptsname grantpt unlockpt)
AC_CHECK_FUNCS(setsid tcflush strcpy)
AC_CHECK_FUNCS(sigprocmask sigsetmask sighold, break)
AC_CHECK_FUNCS(tcsetpgrp)

AC_CHECK_FUNC(gethostname,,AC_MSG_ERROR(Can't run without gethostname))
AC_CHECK_FUNC(select,,AC_MSG_ERROR(Can't compile without select))

AC_OUTPUT(
Makefile
src/Makefile
)
