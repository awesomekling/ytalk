dnl configure.in for YTalk
dnl Process this file with autoconf to produce a configure script.

AC_INIT(YTalk, 3.2.0)
AC_REVISION($LastChangedRevision: 77 $)
AC_CONFIG_HEADER(config.h)

dnl Do some system detection. Don't expand this without a good reason.
host=`uname -s`
case "$host" in
*SunOS*)
  SYSTEM_DEFS=-DYTALK_SUNOS
  ;;
esac
AC_SUBST(SYSTEM_DEFS)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Sanity
AC_CHECK_SIZEOF(long)

dnl Checks for libraries.
AC_CHECK_LIB(ncurses, initscr,, no_ncurses=1)

dnl Curses is tricky; some systems have ncurses, others have curses, and
dnl yet others require -ltermcap with -lcurses.  And using AC_CHECK_LIB
dnl twice w/ the same arguments will use the cache value even if we give
dnl it an "extralibs" argument, so we have to be creative with the function
dnl to look for...
if test "x$no_ncurses" = "x1" ; then
  AC_CHECK_LIB(curses, initscr,, [\
    AC_CHECK_LIB(termcap, tgetstr,, AC_MSG_ERROR(Can't find termcap library))
    AC_CHECK_LIB(curses, endwin,, [\
      AC_MSG_ERROR(Can't find curses or ncurses library)\
    ], -ltermcap)\
  ])
else
  dnl If we do have ncurses, check for ncurses.h 
  AC_CHECK_HEADERS(ncurses.h)
fi

dnl Look for termios, and if that fails, try sgtty.
AC_CHECK_FUNC(tcgetattr, [\
  AC_CHECK_HEADERS(termios.h,, no_termios=1)], no_termios=1)
if test "x$no_termios" = "x1" ; then 
  AC_CHECK_HEADERS(sgtty.h,, AC_MSG_ERROR(Cannot find either termios or sgtty))
fi

dnl X setup.
AC_ARG_WITH(x, 
  AC_HELP_STRING([--with-x], [use the X Window System]),
  no_x=0, no_x=1)
if test "x$no_x" = "x1"; then
  AC_DEFINE([X_DISPLAY_MISSING])
  XLIBS=""
  XINCS=""
else
  AC_PATH_XTRA
  if test "x$X_LIBS" = "x"; then
    echo "No X found but --with-x stated, aborting!"
    exit 1
  fi
  XLIBS="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"
  XINCS="$X_CFLAGS"
  AC_LIBOBJ([xwin])
fi
AC_SUBST(XLIBS)
AC_SUBST(XINCS)

dnl Some systems need extra libraries for sockets and dns...
AC_CHECK_FUNC(socket,,AC_CHECK_LIB(socket, socket,, [AC_MSG_ERROR(Can't compile without socket)]))
AC_CHECK_FUNC(gethostbyname,,AC_CHECK_LIB(nsl, gethostbyname,, [AC_MSG_ERROR(Can't compile without gethostbyname)]))

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h netdb.h netinet/in.h sys/ioctl.h sys/time.h unistd.h stropts.h)
AC_CHECK_HEADERS(iovec.h sys/uio.h stdlib.h string.h sys/param.h arpa/inet.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_HEADER_TIME
AC_TYPE_UID_T

AC_CHECK_TYPE([socklen_t], [AC_DEFINE(HAVE_SOCKLEN_T, 1, [Define to 1 if you have the `socklen_t` type])], [], [#include <sys/socket.h>])

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(putenv strerror ttyname ptsname grantpt unlockpt)
AC_CHECK_FUNCS(setsid tcflush strcpy)
AC_CHECK_FUNCS(sigprocmask sigsetmask sighold, break)
AC_CHECK_FUNCS(tcsetpgrp)

AC_CHECK_FUNC(gethostname,,AC_MSG_ERROR(Can't run without gethostname))
AC_CHECK_FUNC(select,,AC_MSG_ERROR(Can't compile without select))

AC_CONFIG_FILES(Makefile)

AC_OUTPUT
